import java.nio.charset.StandardCharsets

plugins {
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'java'
    id 'com.github.ben-manes.versions' version '0.51.0'
    id 'org.ajoberstar.reckon' version '0.18.3'
}

def generalCharset = StandardCharsets.UTF_8.name()
static def isNonStable(String version) {
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

group = 'me.anisekai'

compileJava.options.encoding = generalCharset
compileTestJava.options.encoding = generalCharset
javadoc.options.encoding = generalCharset

reckon {
    defaultInferredScope = 'patch'
    stages('dev', 'beta', 'rc', 'final')

    scopeCalc = calcScopeFromProp() | calcScopeFromCommitMessages()
    stageCalc = calcStageFromProp()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    set('sentryVersion', "7.6.0")
}

dependencies {
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'io.sentry:sentry-spring-boot:8.0.0-alpha.4'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    runtimeOnly group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '3.4.1'
    implementation group: 'org.flywaydb', name: 'flyway-core', version: '10.18.2'
    implementation group: 'org.flywaydb', name: 'flyway-mysql', version: '10.18.2'

    implementation group: 'net.dv8tion', name: 'JDA', version: '5.1.1'

    implementation group: 'fr.alexpado', name: 'simple-http-client', version: '1.1.2'
    implementation(group: 'fr.alexpado', name: 'jda-interactions-extension', version: '0.1.0-rc.19') {
        exclude(group: 'io.sentry')
    }

    implementation group: 'com.github.slugify', name: 'slugify', version: '3.0.7'
    implementation group: 'org.jsoup', name: 'jsoup', version: '1.18.1'
    implementation group: 'org.json', name: 'json', version: '20240303'

    implementation group: 'org.jetbrains', name: 'annotations', version: '25.0.0'

    implementation group: 'rome', name: 'rome', version: '1.0'
}

dependencyManagement {
    imports {
        mavenBom "io.sentry:sentry-bom:${sentryVersion}"
    }
}

test {
    useJUnitPlatform()
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}

tasks.register('generateJava', Copy) {
    def context = [version: project.version.toString()]
    inputs.properties context
    from 'src/template/java'
    into "$buildDir/generated/java"
    expand context
}

sourceSets.main.java.srcDir "$buildDir/generated/java"
compileJava.dependsOn 'generateJava'